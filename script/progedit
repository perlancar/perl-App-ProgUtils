#!perl

# DATE
# VERSION

use 5.010;
use strict;
use warnings;

use App::ProgUtils;
use Perinci::CmdLine::Any;

our %SPEC;
$SPEC{progedit} = {
    v => 1.1,
    summary => 'Launch editor for program',
    description => <<'_',

Basically

    % progedit PROG

is roughly the same as:

    % $EDITOR `which PROG`

except that it offers tab completion and can also search PROG in the current
directory or normal path (e.g. `progless ../bin/foo`).
_
    args => {
        program => {
            schema => 'str*',
            req => 1,
            pos => 0,
            completion => $App::ProgUtils::_complete_program,
        },
    },
};
sub progedit {
    require String::ShellQuote;

    return [412, "EDITOR is not set"] unless defined($ENV{EDITOR});

    my %args = @_;
    # array variant doesn't always work because $EDITOR can contain args, e.g.
    # "emacsclient -t"
    my $cmd = "$ENV{EDITOR} " . String::ShellQuote::shell_quote(
        App::ProgUtils::_search_program($args{program}));
    exec $cmd;
    # [200]; # unreached
}

Perinci::CmdLine::Any->new(
    url => '/main/progedit',
)->run;

# ABSTRACT:
# PODNAME:

=head1 SYNOPSIS

Basic usage:

 % progedit mpath

