#!/usr/bin/env perl

use 5.010;
use strict;
use warnings;

use Perinci::CmdLine 1.04;

# DATE
# VERSION

our %SPEC;
$SPEC{progman} = {
    v => 1.1,
    summary => 'Show manpage for program',
    description => <<'_',

Basically

    % progman PROG

is the same as:

    % man `which PROG`

except that `progman` offers tab completion.

_
    args => {
        program => {
            schema => 'str*',
            req => 1,
            pos => 0,
            completion => sub {
                require SHARYANTO::Complete::Util;
                my %args = @_;
                SHARYANTO::Complete::Util::mimic_shell_dir_completion(
                    completion => SHARYANTO::Complete::Util::complete_program(
                        word      => $args{word},
                    )
                );
            },
        },
    },
    deps => {
        prog => 'man',
    },
};
sub progman {
    require File::Which;

    my %args = @_;
    exec "man", File::Which::which($args{program});
    # [200]; # unreached
}

Perinci::CmdLine->new(url => '/main/progman')->run;

# ABSTRACT: Show manpage for program
# PODNAME: progman

=head1 SYNOPSIS

Basic usage:

 % progman mpath

To activate bash completion:

 % complete -C progman progman; # can be put in bash startup file e.g. .bashrc
 % progman mpa<tab>


